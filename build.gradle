buildscript {
    repositories {
        maven { url 'http://dl.bintray.com/msgilligan/maven' }
        maven { url 'http://jcenter.bintray.com' }
        maven { url "http://repo.spring.io/libs-snapshot" }
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.0.1.RELEASE")
        classpath 'org.gradle.api.plugins:gradle-cloudbees-plugin:0.1.1'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'war'
apply plugin: 'spring-boot'
apply plugin: 'cloudbees'

archivesBaseName = 'social-transformation'
version = '0.1.1'

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/libs-snapshot" }
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.apache.commons:commons-lang3:3.2.1"
    compile "com.google.guava:guava:16.0.1"
    compile "mysql:mysql-connector-java:5.1.28"
    compile "com.h2database:h2"
    compile "org.thymeleaf:thymeleaf-spring4"
    testCompile "junit:junit"
    testCompile "org.easymock:easymock:3.1"
}

springBoot {
    mainClass = "me.darrenlyons.st.Application"
    backupSource = true
}

def springBootWarLauncherClassName = "org.springframework.boot.loader.WarLauncher"

// Make the deploy target easier to find/type
task deploy(dependsOn: cloudBeesAppDeployArchive)

cloudBees {
    apiKey = 'cloudbeesApiKey'
    apiSecret = 'cloudbeesApiSecret'
    appId = 'community/springboot-sample'
    archiveType = 'jar'
    archiveFile = war.archivePath
    deltaDeploy = true
    parameters = [containerType: "java", 'runtime.class': springBootWarLauncherClassName]
}

cloudBeesAppDeployArchive.dependsOn assemble
cloudBeesAppDeployArchive.message = project.version

tasks.withType(Test) {
    scanForTestClasses = false
    include "**/*Test.class" // whatever Ant pattern matches your test class files
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}